plugins {
    id 'java-library'
    id 'maven-publish'
}

base {
    archivesName = "${libs.versions.modVersion.get()}-${project.name}-${libs.versions.modern.minecraft.get()}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(libs.versions.javaVersion.get())
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups("org.spongepowered") }
    }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements', 'sourcesElements', 'javadocElements'].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:${libs.versions.modVersion.get()}-${project.name}-${libs.versions.modern.minecraft.get()}:$version")
        capability("$group:${libs.versions.modVersion.get()}:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}

sourcesJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${libs}" }
    }
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }

    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_author,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_author,
                'Built-On-Minecraft'    : libs.versions.modern.minecraft.get()
        ])
    }
}

processResources {
    def expandProps = [
            "version"              : version,
            "group"                : project.group, //Else we target the task's group.
            "minecraft_version"    : libs.versions.modern.minecraft.get(),
            "fabric_version"       : libs.versions.modern.fabric.api.get(),
            "fabric_loader_version": libs.versions.modern.fabric.loader.get(),
            "mod_name"             : mod_name,
            "mod_id"               : mod_id,
//            "description": project.description,
            "java_version"         : libs.versions.javaVersion.get()
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url System.getenv("local_maven_url")
        }
    }
}
